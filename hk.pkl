amends "package://github.com/jdx/hk/releases/download/v1.28.0/hk@1.28.0#/Config.pkl"
import "package://github.com/jdx/hk/releases/download/v1.28.0/hk@1.28.0#/Builtins.pkl"

local linters = new Mapping<String, Step> {
    ["ws"] = Builtins.trailing_whitespace
    ["prettier"] = Builtins.prettier
    ["yaml"] = Builtins.yq
    ["sh"] = Builtins.shellcheck
    ["ruby"] = Builtins.rubocop

    // define a custom linter
    ["pkl"] {
        glob = "*.pkl"
        check = "pkl eval {{files}} >/dev/null"
    }
}

hooks {
    ["pre-commit"] {
        fix = true    // automatically modify files with available linter fixes
        stash = "git" // stashes unstaged changes while running fix steps
        steps {
            // "prelint" here is simply a name to define the step
            ["prelint"] {
                // if a step has a "check" script it will execute that
                check = "mise run prelint"
                exclusive = true // ensures that the step runs in isolation
            }
            ...linters // add all linters defined above
            ["postlint"] {
                check = "mise run postlint"
                exclusive = true
            }
        }
    }
    // instead of pre-commit, you can instead define pre-push hooks
    ["pre-push"] {
        steps = linters
    }
    // "fix" and "check" are special steps for `hk fix` and `hk check` commands
    ["fix"] {
        fix = true
        steps = linters
    }
    ["check"] {
        steps = linters
    }
}
